CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(liboi)

INCLUDE("${CMAKE_SOURCE_DIR}/CMakeModules/SetVersion.cmake")

# Compile with C++11 support.
INCLUDE(CheckCXXCompilerFlag)
CHECK_CXX_COMPILER_FLAG("-std=c++11" COMPILER_SUPPORTS_CXX11)
CHECK_CXX_COMPILER_FLAG("-std=c++0x" COMPILER_SUPPORTS_CXX0X)
IF(COMPILER_SUPPORTS_CXX11)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
ELSEIF(COMPILER_SUPPORTS_CXX0X)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++0x")
ELSE()
	MESSAGE(STATUS "The compiler ${CMAKE_CXX_COMPILER} has no C++11 support. Please use a different C++ compiler.")
ENDIF()

#IF("${CMAKE_CXX_COMPILER} ${CMAKE_CXX_COMPILER_ARG1}" MATCHES ".*clang")
#	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -stdlib=libc++")
#ENDIF("${CMAKE_CXX_COMPILER} ${CMAKE_CXX_COMPILER_ARG1}" MATCHES ".*clang")

SET(EXECUTABLE_OUTPUT_PATH ${CMAKE_SOURCE_DIR}/bin)
SET(LIBRARY_OUTPUT_PATH ${CMAKE_SOURCE_DIR}/lib)
LIST(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/CMakeModules)

IF(CMAKE_COMPILER_IS_GNUCC)
	ADD_DEFINITIONS(-Wall -Wextra -Wshadow)
ENDIF()

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/include)

# We need to compile the following libraries
IF(NOT TARGET textio)
	ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/lib/textio EXCLUDE_FROM_ALL)
	INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/lib/textio/src)
ENDIF()

IF(NOT TARGET ccoifits)
	ADD_SUBDIRECTORY(${CMAKE_SOURCE_DIR}/lib/ccoifits EXCLUDE_FROM_ALL)
	INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/lib/ccoifits/src)
	INCLUDE_DIRECTORIES(${CCOIFITS_INCLUDE_DIRS})
ENDIF()

# Include and build Google Test framework:
IF(NOT TARGET gtest)
	ADD_SUBDIRECTORY(lib/gtest-1.6.0)
	ENABLE_TESTING()
ENDIF()
INCLUDE_DIRECTORIES(${gtest_SOURCE_DIR}/include ${gtest_SOURCE_DIR})

ADD_SUBDIRECTORY(src)

# Copy over kernel and shader sources:
FILE(GLOB KERNELS ${CMAKE_CURRENT_SOURCE_DIR}/src/kernels/*.cl)
FILE(COPY ${KERNELS} DESTINATION ${EXECUTABLE_OUTPUT_PATH}/kernels/)

# Automatic packaging
SET(CPACK_GENERATOR "DEB")
SET(CPACK_PACKAGE_NAME "liboi")
SET(CPACK_PACKAGE_VERSION ${GIT_VERSION_TAG})
SET(CPACK_PACKAGE_CONTACT "Brian Kloppenborg <bkloppenborg@gmail.com>")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "OpenCL Interferometry Library (https://github.com/bkloppenborg/liboi)")

# Debian-specific packaging options
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "textio ( >= 1.0.0 ), ccoifits ( >= 1.0.0 ), ocl-icd-libopencl1 ( >= 2.1.3 )")
 
INCLUDE(CPack)

